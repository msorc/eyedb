# 
# configure.in for eyedb-benchmarks
# 

AC_INIT(src/java/org/eyedb/benchmark/StopWatch.java)

PACKAGE_NAME=eyedb-benchmarks
PACKAGE_VERSION=1.0.0

AM_INIT_AUTOMAKE(${PACKAGE_NAME}, ${PACKAGE_VERSION})

# ========================================
# Collect command line arguments
# ========================================
# Java
AC_ARG_ENABLE(java,
		AS_HELP_STRING([--enable-java],
				[enable Java code compilation [default=yes]]),,
		ac_enable_java="yes")
# EyeDB
AC_ARG_ENABLE(eyedb,
		AS_HELP_STRING([--enable-eyedb],
				[enable EyeDB code compilation [default=yes]]),,
		ac_enable_eyedb="yes")
# EyeDB directories
AC_ARG_WITH( eyedb-installdir, 
	     AS_HELP_STRING([--with-eyedb-installdir=DIRECTORY],[gives EyeDB installation directory]),
	     ac_eyedb_installdir="$withval",
	     ac_eyedb_installdir="none")
# EyeDB binaries
AC_ARG_WITH( eyedb-bindir, 
	     AS_HELP_STRING([--with-eyedb-bindir=DIRECTORY],[gives EyeDB binaries directory]),
	     ac_eyedb_bindir="$withval",
	     ac_eyedb_bindir="none")
# EyeDB jar file
AC_ARG_WITH( eyedb-jar, 
	     AS_HELP_STRING([--with-eyedb-jar=FILE],[gives EyeDB jar file location]),
	     ac_eyedb_jar="$withval",
	     ac_eyedb_jar="none")
# Db4o
AC_ARG_ENABLE(db4o,
		AS_HELP_STRING([--enable-db4o],
				[enable db4o code compilation [default=yes]]),,
		ac_enable_db4o="yes")
# hibernate
AC_ARG_ENABLE(hibernate,
		AS_HELP_STRING([--enable-hibernate],
				[enable hibernate code compilation [default=yes]]),,
		ac_enable_hibernate="yes")

# ========================================
# JAVA compilation
# ========================================
if test "$ac_enable_java" = "yes"
then
	# On Mac OS X, if JAVAC is not set, we force it to be javac, because
	# AC_PROG_JAVAC tests first jikes, which is currently broken on Mac OS X
	case "$host" in
	powerpc-apple-darwin*)
		if test x"$JAVAC" = x ; then export JAVAC=javac ; fi
		;;
	esac
	AC_PROG_JAVAC
	export CLASSPATH=.
	AC_PROG_JAVA
	AC_PATH_PROG(JAR, jar)

	if test "$enable_debug" = "yes"
	then
		JAVACFLAGS="$JAVACFLAGS -g"
	fi

fi

if test "$ac_enable_java" = "yes" -a x"$JAVAC" != x -a x"$JAR" != x  ; then
   COND_JAVA="yes"
fi
AM_CONDITIONAL(COND_JAVA, test x"$COND_JAVA" = xyes)

AC_SUBST(JAVACFLAGS)

# ========================================
# Settings for packages
# ========================================

# ========================================
# EyeDB
# ========================================
if test "$ac_enable_eyedb" = "yes"
then
	if test x"$ac_eyedb_bindir" != xnone ; then
	   EYEDB_BINDIR=${ac_eyedb_bindir}
	elif test x"$ac_eyedb_installdir" != xnone ; then
	   EYEDB_BINDIR=${ac_eyedb_installdir}/bin
	fi

	AC_PATH_PROG(EYEDBCTL,eyedbctl,none,${EYEDB_BINDIR:-$PATH})

	if test x$"EYEDBCTL" != xnone ; then
	   EYEDB_BINDIR=`dirname $EYEDBCTL`
	else
	   EYEDB_BINDIR=none
	fi   

	if test x"$ac_eyedb_jar" != xnone ; then
	   EYEDB_JAR=${ac_eyedb_jar}
	else
	   EYEDB_JAR=${ac_eyedb_installdir}/lib/eyedb/java/eyedb.jar
	fi

	AC_CHECK_FILE($EYEDB_JAR,HAVE_EYEDB_JAR="yes",HAVE_EYEDB_JAR="no")
fi

if test "$ac_enable_eyedb" = "yes" -a "$HAVE_EYEDB_JAR" = "yes" -a "$EYEDB_BINDIR" != "none" ; then
   COND_EYEDB="yes"
fi
AM_CONDITIONAL(COND_EYEDB, test x"$COND_EYEDB" = xyes)

AC_SUBST(EYEDB_BINDIR)
AC_SUBST(EYEDB_JAR)

# ========================================
# db4o
# ========================================

AC_ARG_VAR(DB4O_JAR,[DB4O jar file])
# To be changed if DB4O version changes
DB4O_JAR_DEFAULT=${srcdir}/lib/java/db4o/db4o-7.2/lib/db4o-7.2.54.11278-java5.jar

if test "$ac_enable_db4o" = "yes" ; then
   if test x"$DB4O_JAR" = x ; then
	DB4O_JAR=$DB4O_JAR_DEFAULT
   fi
   AC_CHECK_FILE($DB4O_JAR,HAVE_DB4O_JAR="yes",HAVE_DB4O_JAR="no")
fi
if test "$ac_enable_db4o" = "yes" -a "$HAVE_DB4O_JAR" = "yes" ; then
   COND_DB4O="yes"
fi
AM_CONDITIONAL(COND_DB4O, test x"$COND_DB4O" = xyes)

AC_SUBST(DB4O_JAR)


# ========================================
# hibernate
# ========================================

AC_ARG_VAR(HIBERNATE_JAR,[Hibernate jar file])
AC_ARG_VAR(HIBERNATE_LIB_DIR,[Hibernate libraries directory])
AC_ARG_VAR(JDBC_CONNECTOR_JAR,[JDBC connector jar file])
# To be changed if Hibernate or JDBC version changes
HIBERNATE_JAR_DEFAULT=${srcdir}/lib/java/hibernate/hibernate-distribution-3.3.0.SP1/hibernate3.jar
HIBERNATE_LIB_DIR_DEFAULT=${srcdir}/lib/java/hibernate/hibernate-distribution-3.3.0.SP1/lib/required
JDBC_CONNECTOR_JAR_DEFAULT=${srcdir}/lib/java/jdbc/mysql-connector-java-5.1.6/mysql-connector-java-5.1.6-bin.jar

if test "$ac_enable_hibernate" = "yes" ; then

   if test x"$HIBERNATE_JAR" = x ; then
	HIBERNATE_JAR=$HIBERNATE_JAR_DEFAULT
   fi
   AC_CHECK_FILE($HIBERNATE_JAR,HAVE_HIBERNATE_JAR="yes",HAVE_HIBERNATE_JAR="no")
   if test x"$HIBERNATE_LIB_DIR" = x ; then
	HIBERNATE_LIB_DIR=$HIBERNATE_LIB_DIR_DEFAULT
   fi

   HIBERNATE_CLASSPATH=""
   for j in "$HIBERNATE_LIB_DIR"/*.jar ; do 
       HIBERNATE_CLASSPATH="$j:$HIBERNATE_CLASSPATH"
   done

   if test x"$JDBC_CONNECTOR_JAR" = x ; then
	JDBC_CONNECTOR_JAR=$JDBC_CONNECTOR_JAR_DEFAULT
   fi
   AC_CHECK_FILE($JDBC_CONNECTOR_JAR,HAVE_JDBC_CONNECTOR_JAR="yes",HAVE_JDBC_CONNECTOR_JAR="no")
fi

if test "$ac_enable_hibernate" = "yes" -a "$HAVE_HIBERNATE_JAR" = "yes" -a "$HAVE_JDBC_CONNECTOR_JAR" = "yes" ; then
   COND_HIBERNATE="yes"
fi
AM_CONDITIONAL(COND_HIBERNATE, test x"$COND_HIBERNATE" = xyes)

AC_SUBST(HIBERNATE_JAR)
AC_SUBST(HIBERNATE_CLASSPATH)
AC_SUBST(JDBC_CONNECTOR_JAR)


# ========================================
# Generate output files
# ========================================
AC_CONFIG_FILES(
Makefile dnl
src/Makefile dnl
src/java/Makefile dnl
src/java/org/Makefile dnl
src/java/org/eyedb/Makefile dnl
src/java/org/eyedb/benchmark/Makefile dnl
src/java/common/Makefile dnl
src/java/common/db4o/Makefile dnl
src/java/bench1/Makefile dnl
src/java/bench1/eyedb/Makefile dnl
src/java/bench1/db4o/Makefile dnl
src/java/bench1/hibernate/Makefile dnl
src/java/bench2/Makefile dnl
src/java/bench2/eyedb/Makefile dnl
src/java/bench2/db4o/Makefile dnl
src/java/quicktour/Makefile dnl
src/java/quicktour/db4o/Makefile dnl
src/java/quicktour/eyedb/Makefile dnl
src/java/quicktour/hibernate/Makefile dnl
)
AC_OUTPUT
