# 
# configure.in for eyedb-benchmarks
# 

AC_INIT(src/java/org/eyedb/benchmark/StopWatch.java)

PACKAGE_NAME=eyedb-benchmarks
PACKAGE_VERSION=1.0.0

AM_INIT_AUTOMAKE(${PACKAGE_NAME}, ${PACKAGE_VERSION})

#
# Checks for command line flags
#

# ========================================
# Collect arguments
# ========================================
# Java
AC_ARG_ENABLE(java,
		AS_HELP_STRING([--enable-java],
				[enable Java code compilation [default=yes]]),,
		enable_java="yes")
# EyeDB
AC_ARG_ENABLE(eyedb,
		AS_HELP_STRING([--enable-eyedb],
				[enable EyeDB code compilation [default=yes]]),,
		enable_eyedb="yes")
# EyeDB directories
AC_ARG_WITH( eyedb-installdir, 
	     AS_HELP_STRING([--with-eyedb-installdir=DIRECTORY],[gives EyeDB installation directory]),
	     ac_eyedb_installdir="$withval",
	     ac_eyedb_installdir="none")
# Db4o
AC_ARG_ENABLE(db4o,
		AS_HELP_STRING([--enable-db4o],
				[enable db4o code compilation [default=yes]]),,
		enable_db4o="yes")
# db4o files
AC_ARG_WITH( db4o-jar, 
	     AS_HELP_STRING([--with-db4o-jar=FILE],[gives Db4o jar file location]),
	     ac_db4o_jar="$withval",
	     ac_db4o_jar="none")

# ========================================
# JAVA compilation
# ========================================
if test "$enable_java" = "yes"
then
	# On Mac OS X, if JAVAC is not set, we force it to be javac, because
	# AC_PROG_JAVAC tests first jikes, which is currently broken on Mac OS X
	case "$host" in
	powerpc-apple-darwin*)
		if test x"$JAVAC" = x ; then export JAVAC=javac ; fi
		;;
	esac
	AC_PROG_JAVAC
	export CLASSPATH=.
	AC_PROG_JAVA
	AC_PATH_PROG(JAR, jar)

	if test "$enable_debug" = "yes"
	then
		JAVACFLAGS="$JAVACFLAGS -g"
	fi

fi

if test "$enable_java" = "yes" -a x"$JAVAC" != x -a x"$JAR" != x  ; then
   COND_JAVA="yes"
fi
AM_CONDITIONAL(COND_JAVA, test x"$COND_JAVA" = xyes)
AC_SUBST(JAVACFLAGS)

# ========================================
# Settings for packages
# ========================================
# EyeDB
EYEDB_BINDIR=${ac_eyedb_installdir}/bin
EYEDB_LIBDIR=${ac_eyedb_installdir}/lib
EYEDB_JAR=${EYEDB_LIBDIR}/eyedb/java/eyedb.jar
AC_CHECK_FILE($EYEDB_JAR,HAVE_EYEDB_JAR="yes",HAVE_EYEDB_JAR="no")

AC_SUBST(EYEDB_BINDIR)
AC_SUBST(EYEDB_LIBDIR)
AC_SUBST(EYEDB_JAR)

if test "$enable_eyedb" = "yes" -a "$HAVE_EYEDB_JAR" = "yes" ; then
   COND_EYEDB="yes"
fi
AM_CONDITIONAL(COND_EYEDB, test x"$COND_EYEDB" = xyes)

# db4o
DB4O_JAR=${ac_db4o_jar}
AC_CHECK_FILE($DB4O_JAR,HAVE_DB4O_JAR="yes",HAVE_DB4O_JAR="no")

AC_SUBST(DB4O_JAR)

if test "$enable_db4o" = "yes" -a "$HAVE_DB4O_JAR" = "yes" ; then
   COND_DB4O="yes"
fi
AM_CONDITIONAL(COND_DB4O, test x"$COND_DB4O" = xyes)

# ========================================
# Generate output files
# ========================================
AC_OUTPUT(
Makefile
src/Makefile
src/java/Makefile
src/java/org/Makefile
src/java/org/eyedb/Makefile
src/java/org/eyedb/benchmark/Makefile
src/java/bench1/Makefile
src/java/bench1/eyedb/Makefile
src/java/bench1/db4o/Makefile
src/java/bench2/Makefile
src/java/bench2/eyedb/Makefile
src/java/bench2/db4o/Makefile
)
