# 
# configure.in for eyedb-benchmarks
# 

AC_INIT(src/java/org/eyedb/benchmark/framework/StopWatch.java)

PACKAGE_NAME=eyedb-benchmarks
PACKAGE_VERSION=1.0.0

AM_INIT_AUTOMAKE(${PACKAGE_NAME}, ${PACKAGE_VERSION})

# ========================================
# Collect command line arguments
# ========================================
# C++
AC_ARG_ENABLE(cpp,
		AS_HELP_STRING([--enable-cpp],
				[enable C++ code compilation [default=yes]]),,
		ac_enable_cpp="yes")
# Java
AC_ARG_ENABLE(java,
		AS_HELP_STRING([--enable-java],
				[enable Java code compilation [default=yes]]),,
		ac_enable_java="yes")
# EyeDB
AC_ARG_ENABLE(eyedb,
		AS_HELP_STRING([--enable-eyedb],
				[enable EyeDB code compilation [default=yes]]),,
		ac_enable_eyedb="yes")
# Db4o
AC_ARG_ENABLE(db4o,
		AS_HELP_STRING([--enable-db4o],
				[enable db4o code compilation [default=yes]]),,
		ac_enable_db4o="yes")
# hibernate
AC_ARG_ENABLE(hibernate,
		AS_HELP_STRING([--enable-hibernate],
				[enable hibernate code compilation [default=yes]]),,
		ac_enable_hibernate="yes")
# Poleposition
AC_ARG_ENABLE(polepos,
		AS_HELP_STRING([--enable-polepos],
				[enable polepos code compilation [default=yes]]),,
		ac_enable_polepos="yes")


# ========================================
# C++ compilation
# ========================================
if test "$ac_enable_cpp" = "yes"
then
	AC_PROG_CXX
	AC_PROG_LIBTOOL
fi

if test "$ac_enable_cpp" = "yes" -a x"$CXX" != x ; then
   COND_CPP="yes"
fi
AM_CONDITIONAL(COND_CPP, test x"$COND_CPP" = xyes)

# ========================================
# JAVA compilation
# ========================================
if test "$ac_enable_java" = "yes"
then
	# On Mac OS X, if JAVAC is not set, we force it to be javac, because
	# AC_PROG_JAVAC tests first jikes, which is currently broken on Mac OS X
	case "$host" in
	powerpc-apple-darwin*)
		if test x"$JAVAC" = x ; then export JAVAC=javac ; fi
		;;
	esac
	AC_PROG_JAVAC
	export CLASSPATH=.
	AC_PROG_JAVA
	AC_PATH_PROG(JAR, jar)

	if test "$enable_debug" = "yes"
	then
		JAVACFLAGS="$JAVACFLAGS -g"
	fi

fi

if test "$ac_enable_java" = "yes" -a x"$JAVAC" != x -a x"$JAR" != x ; then
   COND_JAVA="yes"
fi
AM_CONDITIONAL(COND_JAVA, test x"$COND_JAVA" = xyes)

AC_SUBST(JAVACFLAGS)

# ========================================
# Settings for packages
# ========================================

# ========================================
# EyeDB
# ========================================
AC_ARG_VAR(EYEDB_INSTALLDIR,[EyeDB installation directory])
EYEDB_INSTALLDIR_DEFAULT=/usr

if test "$ac_enable_eyedb" = "yes"
then
   if test x"$EYEDB_INSTALLDIR" = x ; then
	EYEDB_INSTALLDIR=$EYEDB_INSTALLDIR_DEFAULT
   fi
   EYEDB_BINDIR=$EYEDB_INSTALLDIR/bin
   EYEDB_SBINDIR=$EYEDB_INSTALLDIR/sbin
   EYEDB_INCLUDEDIR=$EYEDB_INSTALLDIR/include
   EYEDB_LIBDIR=$EYEDB_INSTALLDIR/lib

   EYEDB_JAR=${EYEDB_INSTALLDIR}/lib/eyedb/java/eyedb.jar
   AC_CHECK_FILE($EYEDB_JAR,HAVE_EYEDB_JAR="yes",HAVE_EYEDB_JAR="no")
fi

if test "$ac_enable_eyedb" = "yes" -a "$HAVE_EYEDB_JAR" = "yes" ; then
   COND_EYEDB="yes"
fi
AM_CONDITIONAL(COND_EYEDB, test x"$COND_EYEDB" = xyes)

AC_SUBST(EYEDB_BINDIR)
AC_SUBST(EYEDB_SBINDIR)
AC_SUBST(EYEDB_INCLUDEDIR)
AC_SUBST(EYEDB_LIBDIR)
AC_SUBST(EYEDB_JAR)

# ========================================
# db4o
# ========================================

AC_ARG_VAR(DB4O_JAR,[DB4O jar file])

# To be changed if DB4O version changes
DB4O_JAR_DEFAULT=lib/java/db4o/db4o-7.2/lib/db4o-7.2.54.11278-java5.jar

if test "$ac_enable_db4o" = "yes" ; then
   if test x"$DB4O_JAR" = x ; then
	DB4O_JAR='${abs_top_srcdir}'/$DB4O_JAR_DEFAULT
	HAVE_DB4O_JAR="yes"
   else
	AC_CHECK_FILE($DB4O_JAR,HAVE_DB4O_JAR="yes",HAVE_DB4O_JAR="no")
   fi
fi
if test "$ac_enable_db4o" = "yes" -a "$HAVE_DB4O_JAR" = "yes" ; then
   COND_DB4O="yes"
fi
AM_CONDITIONAL(COND_DB4O, test x"$COND_DB4O" = xyes)

AC_SUBST(DB4O_JAR)


# ========================================
# hibernate
# ========================================

AC_ARG_VAR(HIBERNATE_JAR,[Hibernate jar file])
AC_ARG_VAR(JDBC_CONNECTOR_JAR,[JDBC connector jar file])
AC_ARG_VAR(SLF4J_CONNECTOR_JAR,[SLF4J jar file])
AC_ARG_VAR(HIBERNATE_LIBDIR,[Hibernate libraries directory])

# To be changed if Hibernate or JDBC version changes
HIBERNATE_JAR_DEFAULT=lib/java/hibernate/hibernate-distribution-3.3.0.SP1/hibernate3.jar
JDBC_CONNECTOR_JAR_DEFAULT=lib/java/jdbc/mysql-connector-java-5.1.6/mysql-connector-java-5.1.6-bin.jar
SLF4J_JAR_DEFAULT=lib/java/slf4j/slf4j-1.5.2/slf4j-simple-1.5.2.jar
HIBERNATE_LIBDIR_DEFAULT=lib/java/hibernate/hibernate-distribution-3.3.0.SP1/lib/required

if test "$ac_enable_hibernate" = "yes" ; then

   if test x"$HIBERNATE_JAR" = x ; then
	HIBERNATE_JAR='${abs_top_srcdir}'/$HIBERNATE_JAR_DEFAULT
	HAVE_HIBERNATE_JAR="yes"
   else
	AC_CHECK_FILE($HIBERNATE_JAR,HAVE_HIBERNATE_JAR="yes",HAVE_HIBERNATE_JAR="no")
   fi

   if test x"$JDBC_CONNECTOR_JAR" = x ; then
	JDBC_CONNECTOR_JAR='${abs_top_srcdir}'/$JDBC_CONNECTOR_JAR_DEFAULT
	HAVE_JDBC_CONNECTOR_JAR="yes"
   else
	AC_CHECK_FILE($JDBC_CONNECTOR_JAR,HAVE_JDBC_CONNECTOR_JAR="yes",HAVE_JDBC_CONNECTOR_JAR="no")
   fi

   if test x"$SLF4J_JAR" = x ; then
	SLF4J_JAR='${top_srcdir}'/$SLF4J_JAR_DEFAULT
	HAVE_SLF4J_JAR="yes"
   else
	AC_CHECK_FILE($SLF4J_JAR,HAVE_SLF4J_JAR="yes",HAVE_SLF4J_JAR="no")
   fi

   HIBERNATE_CLASSPATH=""
   if test x"$HIBERNATE_LIBDIR" = x ; then
	HIBERNATE_LIBDIR=$HIBERNATE_LIBDIR_DEFAULT
	for j in "${srcdir}/$HIBERNATE_LIBDIR"/*.jar ; do 
	    j=`echo $j | sed -e "s+^${srcdir}/++"`
       	    HIBERNATE_CLASSPATH='${abs_top_srcdir}'/$j:$HIBERNATE_CLASSPATH
	done
   else
	for j in "$HIBERNATE_LIBDIR"/*.jar ; do 
       	    HIBERNATE_CLASSPATH="$j:$HIBERNATE_CLASSPATH"
	done
   fi
   HIBERNATE_CLASSPATH="$HIBERNATE_CLASSPATH:$JDBC_CONNECTOR_JAR:$SLF4J_JAR"

fi

if test "$ac_enable_hibernate" = "yes" -a "$HAVE_HIBERNATE_JAR" = "yes" -a "$HAVE_JDBC_CONNECTOR_JAR" = "yes" ; then
   COND_HIBERNATE="yes"
fi
AM_CONDITIONAL(COND_HIBERNATE, test x"$COND_HIBERNATE" = xyes)

AC_SUBST(HIBERNATE_JAR)
AC_SUBST(HIBERNATE_CLASSPATH)


# ========================================
# Poleposition benchmarks
# ========================================

AC_ARG_VAR(POLEPOS_DIR,[Poleposition benchmarks source directory])

if test "$ac_enable_polepos" = "yes" ; then
   if test x"$POLEPOS_DIR" = x ; then
	HAVE_POLEPOS="no"
   else
	AC_CHECK_FILE($POLEPOS_DIR,HAVE_POLEPOS="yes",HAVE_POLEPOS="no")
   fi
fi
if test "$ac_enable_polepos" = "yes" -a "$HAVE_POLEPOS" = "yes" ; then
   COND_POLEPOS="yes"
fi
AM_CONDITIONAL(COND_POLEPOS, test x"$COND_POLEPOS" = xyes)

AC_SUBST(POLEPOS_DIR)


# ========================================
# Generate output files
# ========================================
AC_CONFIG_LINKS( ${srcdir}/src/cpp/polepos/eyedb/polepos.odl:${srcdir}/src/java/org/eyedb/benchmark/polepos/teams/eyedb/data/data.odl )

AC_CONFIG_FILES(
Makefile dnl
run/Makefile dnl
run/cpp/Makefile dnl
run/cpp/polepos/Makefile dnl
run/cpp/polepos/eyedb/Makefile dnl
run/java/Makefile dnl
run/java/polepos/Makefile dnl
run/java/polepos/eyedb/Makefile dnl
run/java/quicktour/Makefile dnl
run/java/quicktour/db4o/Makefile dnl
run/java/quicktour/eyedb/Makefile dnl
run/java/quicktour/hibernate/Makefile dnl
run/utils/db4o/functions dnl
run/utils/eyedb/functions dnl
src/Makefile dnl
src/cpp/Makefile dnl
src/cpp/benchmark/Makefile dnl
src/cpp/polepos/Makefile dnl
src/cpp/polepos/eyedb/Makefile dnl
src/java/Makefile dnl
src/java/org/Makefile dnl
src/java/org/eyedb/Makefile dnl
src/java/org/eyedb/benchmark/Makefile dnl
src/java/org/eyedb/benchmark/framework/Makefile dnl
src/java/org/eyedb/benchmark/polepos/Makefile dnl
src/java/org/eyedb/benchmark/polepos/teams/Makefile dnl
src/java/org/eyedb/benchmark/polepos/teams/eyedb/Makefile dnl
src/java/org/eyedb/benchmark/polepos/teams/eyedb/data/Makefile dnl
src/java/org/eyedb/benchmark/quicktour/Makefile dnl
src/java/org/eyedb/benchmark/quicktour/db4o/Makefile dnl
src/java/org/eyedb/benchmark/quicktour/eyedb/Makefile dnl
src/java/org/eyedb/benchmark/quicktour/eyedb/quicktour/Makefile dnl
src/java/org/eyedb/benchmark/quicktour/hibernate/Makefile dnl
src/java/org/eyedb/benchmark/utils/Makefile dnl
src/java/org/eyedb/benchmark/utils/db4o/Makefile dnl
src/java/bench1/Makefile dnl
src/java/bench1/eyedb/Makefile dnl
src/java/bench1/db4o/Makefile dnl
src/java/bench1/hibernate/Makefile dnl
src/java/bench2/Makefile dnl
src/java/bench2/eyedb/Makefile dnl
src/java/bench2/db4o/Makefile dnl
)
AC_OUTPUT
